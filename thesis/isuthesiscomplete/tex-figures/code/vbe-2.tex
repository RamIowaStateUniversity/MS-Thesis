\begin{figure}[ht!]
% \centering
\begin{lstlisting}[numbers=left, tabsize=4, escapechar=@, caption={Very busy expression},label={lst:vbe-code},firstline=46, firstnumber=46,lastline=101] 
m: output collection[string][int] of string;
p: Project = input;

type T1 = {in: set of set of string, out: set of set of string};

allExprset: set of set of string;

cfg: CFG;
exitId : int;
str: set of string;

node_expr := visitor {
	before node: Expression -> {
		switch (node.kind) {
			case ExpressionKind.LT,ExpressionKind.OP_ADD, ExpressionKind.OP_SUB, ExpressionKind.OP_INC, ExpressionKind.OP_MULT, ExpressionKind.OP_DIV, ExpressionKind.OP_MOD, ExpressionKind.OP_DEC, ExpressionKind.GT, ExpressionKind.EQ, ExpressionKind.NEQ, ExpressionKind.LTEQ, ExpressionKind.GTEQ, ExpressionKind.LOGICAL_NOT, ExpressionKind.LOGICAL_AND, ExpressionKind.LOGICAL_OR, ExpressionKind.BIT_AND, ExpressionKind.BIT_OR, ExpressionKind.BIT_NOT, ExpressionKind.BIT_XOR, ExpressionKind.BIT_LSHIFT, ExpressionKind.BIT_RSHIFT, ExpressionKind.BIT_UNSIGNEDRSHIFT:
				add(str, string(node.kind)); 
				foreach(j:int;def(node.expressions[j])) {
					visit(node.expressions[j]);
				}
				break;
			case ExpressionKind.ASSIGN:
				foreach(j:int;def(node.expressions[j])) {
					if(j!=0) {
						visit(node.expressions[j]);
					}
				}
				break;
			case ExpressionKind.VARACCESS:
				add(str, node.variable);
				break;
			case ExpressionKind.VARDECL:
				visit(node.variable_decls[0].initializer);
				break;
			case ExpressionKind.LITERAL:
				add(str, node.literal);
				break;
			case ExpressionKind.METHODCALL:
				clear(str);
				stop;
			default:break;
		}
		stop;
	}
};

allExprTraversal := traversal(node: CFGNode): set of string {
	init_set : set of string;
	genset : set of string;
	str = init_set;
	if(def(node.expr)) {
		visit(node.expr, node_expr);
		if(len(str)!=0) {
			add(allExprset, setClone(str));
			genset = setClone(str);
		}
	}
	return genset;
};

collectKillTraversal := traversal(node: CFGNode): set of set of string {
	init_set : set of set of string;
	killset : set of set of string;
	killset = init_set;
	if(def(node.defVariables)) {
		foreach(aa:set of string=allExprset) {
			if(contains(aa,node.defVariables))
				add(killset, aa);
		}
	}
	return setClone(killset);
};

avail_expr := traversal(node: CFGNode): T1 {
	in_set : set of set of string;
	out_set : set of set of string;
	cur_val : T1;
	if(node.id == exitId)
		cur_val = {in_set, out_set};
	else
		cur_val = {in_set, setClone(allExprset)};

	if(def(getValue(node))) {
		cur_val1 := getValue(node);
		cur_val = clone(cur_val1);
	}
	succs := node.successors;
	cur_gen := getValue(node, allExprTraversal);
	cur_kill := getValue(node, collectKillTraversal);
	foreach(succ_node:CFGNode=succs) {
		succ := getValue(succ_node);
		if(def(succ))
			cur_val.out = intersection1(cur_val.out, succ.in);
	}
	cur_val.in = setClone(cur_val.out);
	removeAll(cur_val.in, cur_kill);
	if(len(cur_gen)!=0)
		add(cur_val.in, cur_gen);
	return cur_val;
};

fixp1 := fixp(curr, prev: T1) : bool {
	if (difference1(curr.in, prev.in) == 0)
		return true;	
	return false;
};

def := visitor {
	before node: Method -> {
		clear(allExprTraversal);clear(avail_expr);clear(collectKillTraversal);clear(allExprset);

		cfg = getcfg(node);
		exitId = len(cfg.nodes) - 1;
		traverse(cfg, TraversalDirection.BACKWARD, TraversalKind.HYBRID, allExprTraversal);
		traverse(cfg, TraversalDirection.BACKWARD, TraversalKind.HYBRID, collectKillTraversal);
		traverse(cfg, TraversalDirection.BACKWARD, TraversalKind.HYBRID, avail_expr, fixp1);
	}
};

visit(p, def);
\end{lstlisting}
\end{figure}