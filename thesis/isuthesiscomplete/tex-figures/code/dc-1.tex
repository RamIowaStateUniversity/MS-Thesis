\begin{figure}[ht!]
% \centering
\begin{lstlisting}[numbers=left, tabsize=4, escapechar=@, caption={Dead code},label={lst:dead-code}, lastline = 55] 
m: output collection[string][string][int] of int;
p: Project = input;

type T_gen_kill= {gen: set of string, kill: string, dummy : int};
type T_inout= {in: set of string, out: set of string};

cfg: CFG;


cur_cfg_node: CFGNode;
dead_nodes : set of int;
prev_dead : set of int;
genset : set of string;
killset : set of string;

init := traversal(node: CFGNode): T_gen_kill {
	cur_value : T_gen_kill;
	cur_value = {node.useVariables, node.defVariables, 0};
	return cur_value;
};


live := traversal(node: CFGNode): T_inout {
	in_set : set of string;
	out_set : set of string;
	cur_val : T_inout = {in_set, out_set};
	succs := node.successors;
	if(!contains(dead_nodes, int(node.id))) {
		if(def(getValue(node))) {
			cur_val1 := getValue(node);
			cur_val = clone(cur_val1);
		}
	}
	foreach(succ_node:CFGNode=succs) {
		succ := getValue(succ_node);
		if(def(succ)) {
			cur_val.out = union(cur_val.out,succ.in);
		}
	}
	cur_val.in = setClone(cur_val.out);
	if(!contains(dead_nodes, int(node.id))) {
		gen_kill := getValue(node, init);
		remove(cur_val.in, gen_kill.kill);
		cur_val.in = union(gen_kill.gen, cur_val.in);
	}
	return cur_val;
};


deadTraversal := traversal(node: CFGNode) {
	if(node.defVariables != "") {
		cur_val := getValue(node, live);
		if(def(cur_val)) {
			if(!contains(cur_val.out, node.defVariables)) {
				add(dead_nodes, int(node.id));
			}
		}
	}
};


fixp1 := fixp(curr, prev: T_inout) : bool {
	if (difference(curr.in, prev.in) == 0)
		return true;	
	return false;
};

q_all := visitor {
	before node: Method -> {
		
		cfg = getcfg(node);
		clear(init);clear(dead_nodes);clear(live);clear(prev_dead);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, init);
		do {
			clear(live);
			if(cfg.isValid==0) {
			traverse(cfg, TraversalDirection.BACKWARD, TraversalKind.HYBRID, live, fixp1);
			}
			prev_dead = setIntClone(dead_nodes);
			traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, deadTraversal);
		}while(difference2(dead_nodes,prev_dead)!=0);
		
	}

};

visit(p, q_all);
\end{lstlisting}
\end{figure}