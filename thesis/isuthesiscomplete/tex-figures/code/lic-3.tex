\begin{figure}[ht!]
% \centering
\begin{lstlisting}[numbers=left, tabsize=4, caption={Loop invariant code motion},label={lst:lic-code}, firstline = 108, firstnumber = 108] 
p: Project = input;
m: output collection[string][int] of string;

type T= {in: set of string, out: set of string};
type T1= {gen: string, kill: string};

cfg: CFG;
type Tloop = {start : int, end : int, use : set of string};
type finiteLoop = {start : int, end : int};

loops : set of Tloop;
finiteloops : set of finiteLoop;
codemotion : set of int;
flag := false;

cfg_def := traversal(node: CFGNode) : T1 {
	cur_val : T1 = {"",""};
	if(node.defVariables!="") {
		cur_val.gen = node.defVariables+"@"+string(node.id);
		cur_val.kill = node.defVariables;
	}
	return cur_val;
};

cfg_reach_def := traversal(n: CFGNode): T {
	preds := n.predecessors;
	in_set : set of string;
	out_set : set of string;
	cur_val : T = {in_set, out_set};
	if(def(getValue(n))) {
		cur_val1 := getValue(n);
		cur_val = clone(cur_val1);
	}
	foreach(pred_node:CFGNode=preds) {	
		pred := getValue(pred_node);
		if(def(pred))
			cur_val.in = union(cur_val.in, pred.out);
	}
	cur_val.out = setClone(cur_val.in);
	genkill := getValue(n, cfg_def);
	if(genkill.kill!="") {
		tmp_out:=setClone(cur_val.out);
		foreach(tmp:string=tmp_out) {
			tmp1:=stringClone(tmp);
			str_array:=splitall(tmp1,"@");
			if(str_array[0] == genkill.kill) {
				remove(cur_val.out, tmp1);
			}
		}
		add(cur_val.out, genkill.gen);
	}
	return cur_val;
};


fixp1 := fixp(curr, prev: T) : bool {
	if (difference(curr.out, prev.out) == 0)
		return true;	
	return false;
};


init := traversal(node: CFGNode){
	init_set : set of string;
	cur_val : Tloop = {-1, -1, init_set};
	if(node.name == "WHILE" || node.name == "FOR") {
		succs := node.successors;
		foreach(succ:CFGNode=succs) {
			if(succ.id > node.id + 1) {
				cur_val.start= int(node.id);
				cur_val.end= int(succ.id);
				cur_val.use = node.useVariables;
				add(loops, cur_val);
			}		
		}
	}
};


loop_invariant := traversal(node: CFGNode) {
	foreach(temp:Tloop=loops) {
		flag = false;
		if(node.id > temp.start && node.id < temp.end) {
			if(len(node.useVariables)!=0) {
				cur_val := getValue(node, cfg_reach_def);
				if(def(cur_val)) {
				foreach(use_str:string=node.useVariables) {
					foreach(tmp_str:string=cur_val.out) {
						str_array:=splitall(tmp_str,"@");
						if(str_array[0]==use_str) {
							if(int(str_array[1]) > temp.start) {
								flag = true;
								break;
							}
						}
					}
				}
				}
			}
			if(!flag) {
				add(codemotion, int(node.id));
			}
		}
	}
};

reach_def := visitor {
	before node: Method -> {
		clear(cfg_def);clear(cfg_reach_def);clear(init);clear(loops);clear(codemotion);clear(loop_invariant);

		cfg = getcfg(node);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, cfg_def);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, init);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, cfg_reach_def, fixp1);
		traverse(cfg, TraversalDirection.FORWARD, TraversalKind.HYBRID, loop_invariant);
			
	}
};

visit(p, reach_def);
\end{lstlisting}
\end{figure}